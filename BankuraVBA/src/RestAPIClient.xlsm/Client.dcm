VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "Client"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Option Explicit

'*/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/
'*
'* [機能名] メイン機能
'* [詳  細] 本RestAPIClientのメインとなる処理を実装する。
'*
'* @author Bankura
'* Copyright (c) 2019 Bankura
'*/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/

'******************************************************************************
'* [概  要] メイン処理。
'* [詳  細] 本RestAPIClientのメイン処理。「送信」ボタンより呼び出される。
'*
'******************************************************************************
Sub Main()
    Dim method As String
    Dim url As String
    Dim strReqHdrParams() As String
    Dim reqBody As String
    Dim mySh As Worksheet
    Set mySh = Client
    
    ' 結果出力欄のクリア
    mySh.Range("N7").Value = ""
    mySh.Range("M10").Value = ""
    mySh.Range("M27").Value = ""
    
    ' 各設定値の取得（Method、URL、BODY）
    method = mySh.Range("F7").Value
    url = mySh.Range("H7").Value & mySh.Range("I7").Value
    reqBody = mySh.Range("K10").Value
    
    ' 各設定値の取得（Header）
    Dim pos As Long, cnt1 As Long: cnt1 = 0
    For pos = 11 To 15
        If mySh.Cells(pos, 4).Value = "○" Then
            cnt1 = cnt1 + 1
        End If
    Next pos
    If cnt1 > 0 Then
        ReDim strReqHdrParams(0 To (cnt1 - 1), 0 To 1) As String
    
        Dim cnt2 As Long: cnt2 = 0
    
        For pos = 11 To 15
            If mySh.Cells(pos, 4).Value = "○" Then
                strReqHdrParams(cnt2, 0) = mySh.Cells(pos, 5).Value
                strReqHdrParams(cnt2, 1) = mySh.Cells(pos, 6).Value
                cnt2 = cnt2 + 1
             End If
        Next pos
    End If

    ' 各設定値の取得（Query Parameters）
    Dim pos2 As Long: pos2 = 0
    Dim cnt3 As Long: cnt3 = 0
    Dim params As String
    For pos2 = 19 To 118
        If mySh.Cells(pos2, 4).Value = "○" Then
            cnt3 = cnt3 + 1
            If cnt3 = 1 Then
                params = "?"
            Else
                params = params & "&"
            End If
            params = params & mySh.Cells(pos2, 5).Value & "=" _
            & EncodeURL(mySh.Cells(pos2, 6).Value)
                     
        End If
    Next pos2
    url = url & params
    
    ' RestAPI実行
    Dim ret As Response
    Set ret = SendRequest(method, url, strReqHdrParams, reqBody)
    
    ' 結果出力
    mySh.Range("N7").Value = ret.StatusCd
    mySh.Range("M10").Value = ret.Headers
    mySh.Range("M27").Value = ret.Body

End Sub


