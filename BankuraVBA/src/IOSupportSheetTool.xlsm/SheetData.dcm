VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "SheetData"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Option Explicit


Private Sub Worksheet_Change(ByVal Target As Range)
    On Error GoTo ErrorHandler
    If dsKoubanCol = 0 Then
        Call MyInit
    End If
    If Target.row >= dsStartRow Then
        Call MyUnprotectSheet
        Dim rng As Range
        For Each rng In Target.Rows
            If WorksheetFunction.CountA(Me.Range(Me.Cells(rng.row, dsStartCol), Me.Cells(rng.row, dsStartCol + dsItemCount - 2))) > 0 Then
                Me.Cells(rng.row, dsKoubanCol).Value = rng.row - dsStartRow + 1
            Else
                Me.Cells(rng.row, dsKoubanCol).Value = ""
            End If
        Next
        Call MyProtectSheet
    End If

    Exit Sub
ErrorHandler:
    Call ErrorProcess
End Sub

Private Sub MyUnprotectSheet()
    With Application
        .EnableEvents = False
    End With
    
    'シート保護解除
    If TOOL_PASSWORD = "" Then
        Me.Unprotect
    Else
        Me.Unprotect Password:=TOOL_PASSWORD
    End If
End Sub

Private Sub MyProtectSheet()
    'シート保護
    If TOOL_PASSWORD = "" Then
        Me.Protect
    Else
        Me.Protect Password:=TOOL_PASSWORD
    End If
    
    With Application
        .EnableEvents = True
    End With
End Sub
